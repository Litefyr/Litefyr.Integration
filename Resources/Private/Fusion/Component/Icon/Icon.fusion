prototype(Litespeed.Theme:Component.Icon) < prototype(Neos.Fusion:Component) {
    attributes = Neos.Fusion:DataStructure {
        aria-hidden = 'true'
        focusable = 'false'
        role = 'img'
    }

    styleAndIcon = null
    styleAndIcon.@process.split = ${value ? String.split(value, ':') : null}

    style = null
    style.@process.styleAndIcon = ${value || (this.styleAndIcon ? Array.first(this.styleAndIcon) : 'solid')}

    icon = null
    icon.@process.styleAndIcon = ${value || (this.styleAndIcon ? Array.last(this.styleAndIcon) : null)}

    customIcons = ${['matrix', 'element', 'mattermost']}

    renderer = Neos.Fusion:Case {
        customIcon {
            condition = ${Array.indexOf(props.customIcons, props.icon) != -1}
            renderer = afx`
                <Neos.Fusion:Augmenter
                    {...props.attributes}
                    class={Carbon.String.classNames("svg-inline--fa", props.attributes.class)}
                    content={StaticResource.content('Litespeed.Theme', 'Private/Fusion/Component/Icon/Assets/' + props.icon + '.svg')}
                />
            `
        }
        default {
            condition = ${props.style && props.icon}
            renderer = afx`<Garagist.Fontawesome:Component.Icon {...props} />`
        }
    }
}
